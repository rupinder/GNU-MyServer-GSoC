AC_INIT(MyServer,0.8)
AM_CONFIG_HEADER(config.h)
CFLAGS="${CFLAGS=}"
CXXFLAGS="${CXXFLAGS=}"
LDFLAGS="${LDFLAGS=}"
AC_CANONICAL_HOST
AM_INIT_AUTOMAKE
AC_PROG_CC
AC_PROG_CPP
AC_AIX
AC_ISC_POSIX
AC_MINIX
AC_HEADER_STDC
AC_PROG_CXX
AC_PROG_CXXCPP
AC_PROG_RANLIB

AC_SUBST(CFLAGS)
AC_SUBST(CXXFLAGS)
AC_SUBST(LDFLAGS)

#dnl Options
#AM_OPTIONS_WXCONFIG

dnl Look for argp.h
AC_CHECK_HEADER([argp.h], AC_DEFINE(ARGP, 1, [Define if GNU argp tools are avaible]))

dnl Look for pthreads
GNOME_PTHREAD_CHECK
if test x$PTHREAD_LIB = x; then
   AC_MSG_ERROR([
*** This system appears to have no posix threads
*** Threads are required for MyServer
])
fi
AC_DEFINE(HAVE_PTHREAD, 1, [POSIX threads required for MyServer in a non-windows envorment])

dnl See if we need -lxnet for arpa
AC_CHECK_LIB(xnet, accept, have_xnet=yes)
if test x$have_xnet = xyes; then
   XNET_LIB="-lxnet"
else
   XNET_LIB=""
fi
AC_SUBST(XNET_LIB)

dnl Look for dl
AC_CHECK_LIB(dl ,dlopen, have_dl=yes)

if test x$have_dl != xyes; then
   AC_MSG_WARN([
*** dlLIB not found
*** MSCGI support will not function
])
   AC_DEFINE(DO_NOT_USE_MSCGI, 1, [MSCGI support in MyServer, define to 1 to disable])
   DL_LIB=""
   MAKE_MSCGI=no
else
   MAKE_MSCGI=yes
   AC_DEFINE(HAVE_DL, 1, [LibDL support, define to 1 to enable])
   DL_LIB="-ldl"
fi
AC_SUBST(DL_LIB)

dnl Look for XML2
AM_PATH_XML2(2.6.0, have_xml2=yes)
if test x$have_xml2 != xyes; then
    AC_MSG_ERROR([
*** Cannot find libXML2
*** libXML2 is required for MyServer
])
fi

dnl Look for OpenSSL
AC_CHECK_LIB(ssl, SSL_accept, have_ssl=yes)
if test x$have_ssl != xyes; then
   AC_MSG_WARN([
*** Cannot find OpenSSL
*** HTTPS and other SSL protocols will not be enabled
])
    AC_DEFINE(DO_NOT_USE_SSL, 1, [SSL suport in MyServer, define to 1 to disable])
    OPENSSL_LIB=""
else
    OPENSSL_LIB="-lssl"
fi
AC_SUBST(OPENSSL_LIB)

dnl Look for Zlib
AC_CHECK_LIB(z, uncompress, have_libz=yes)
if test x$have_libz != xyes; then
    AC_MSG_WARN([
*** Cannot find Zlib (http://www.gzip.org/zlib/)
*** gzip file transfer will not be enabled
])
    AC_DEFINE(DO_NOT_USE_GZIP, 1, [Zlib compression, define to 1 to disable])
    ZLIB_LIB=""
else
    ZLIB_LIB="-lz"
    dnl Be sure that it is a good vr
    AC_CHECK_LIB(z, compressBound, have_libz=yes)
    if test x$have_libz != xyes; then
        AC_MSG_WARN([
*** Zlib is too old (http://www.gzip.org/zlib/)
*** compressBound checking will not be enabled
])
    else
        AC_DEFINE(GZIP_CHECK_BOUNDS, 1, [Memory checking for Zlib, not defined in older vrs])
    fi
fi
AC_SUBST(ZLIB_LIB)

dnl Look for libPNG
AM_PATH_LIBPNG(1.2.5, have_png=yes)
if test x$have_png != xyes; then
    AC_MSG_WARN([
*** Cannot find libPNG
*** counter MSCGI example will not be built
])
    MAKE_COUNTER=no
else
    MAKE_COUNTER=yes
fi

#dnl Look for wxwindows
#AM_PATH_WXCONFIG(2.4.0, have_wxwindows=yes)
#if test x$have_wxwindows != xyes; then
#    AC_MSG_WARN([
#*** Cannot find wxWindows
#*** control program will not be built
#])
#    MAKE_CONTROL=no
#else
#    MAKE_CONTROL=yes
#fi

dnl Look for FLTK
AM_PATH_FLTK(1.1.0, have_fltk=yes)
if test x$have_fltk != xyes; then
    AC_MSG_WARN([
*** Cannot find FLTK (www.fltk.org)
*** control program will not be built
])
    MAKE_CONTROL=no
else
    MAKE_CONTROL=yes
fi

AM_CONDITIONAL(BUILD_MSCGI, test "$MAKE_MSCGI" = "yes")
AM_CONDITIONAL(BUILD_COUNTER, test "$MAKE_COUNTER" = "yes")
AM_CONDITIONAL(BUILD_CONTROL, test "$MAKE_CONTROL" = "yes")

dnl AC_CONFIG_SUBDIRS()
AC_CONFIG_FILES(Makefile \
		binaries/Makefile \
		binaries/certificates/Makefile \
		binaries/external/Makefile \
                binaries/languages/Makefile \
		binaries/languages/configure/Makefile \
		binaries/web/Makefile \
                binaries/web/downloads/Makefile \
                binaries/web/cgi-bin/Makefile \
		binaries/web/cgi-src/Makefile \
		binaries/web/cgi-src/post/Makefile \
		binaries/web/cgi-src/counter/Makefile \
		binaries/web/cgi-src/math_sum/Makefile \
		binaries/logs/Makefile \
		binaries/system/Makefile \
		control/Makefile \
		contrib/Makefile \
		contrib/rx/Makefile \
		Documentation/Makefile \
		Documentation/En/Makefile \
		Documentation/En/images/Makefile \
		source/Makefile \
		CGI-LIB/Makefile)
AC_OUTPUT
